plugins {
  id 'org.springframework.boot' version '2.1.8.RELEASE'
  id 'com.bmuschko.docker-spring-boot-application' version '5.0.0'
  id 'nebula.override' version '3.0.2'
}

apply plugin: 'io.spring.dependency-management'

repositories {
  mavenCentral()
  // Needed because rsocket imports the reactor-bom Dysprosium still in milestones
  maven { url 'https://repo.spring.io/milestone' }
}

publishing {
  publications {
    nebula(MavenPublication) {
      artifacts {
        // ensure the Spring Boot jar gets published rather than the plain jar
        artifact bootJar
      }
    }
  }
}

dependencies {
  implementation 'org.springframework.boot:spring-boot-starter-actuator'
  implementation 'org.springframework.boot:spring-boot-starter-webflux'
  implementation 'org.pcollections:pcollections:latest.release'
  implementation 'io.micrometer:micrometer-registry-prometheus'
  implementation 'io.rsocket:rsocket-micrometer:1.0.0-RC3'
  implementation 'io.rsocket:rsocket-transport-netty:1.0.0-RC3'
  implementation 'org.xerial.snappy:snappy-java:latest.release'
  annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

  testImplementation 'org.springframework.boot:spring-boot-starter-test'
  testImplementation 'io.projectreactor:reactor-test'
}

if (hasProperty('dockerUser') && hasProperty('dockerPassword')) {
  docker {
    registryCredentials {
      username = dockerUser
      password = dockerPassword
      email = 'tludwig@pivotal.io'
    }

    springBootApplication {
      tag = "$dockerUser/prometheus-rsocket-proxy:${project.version}"
      baseImage = 'openjdk:8'
    }
  }
}
