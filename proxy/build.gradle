plugins {
  id 'org.springframework.boot' version '2.7.2'
}

apply plugin: 'io.spring.dependency-management'

repositories {
  mavenCentral()
}

publishing {
  publications {
    nebula(MavenPublication) {
      artifacts {
        // ensure the Spring Boot jar gets published rather than the plain jar
        artifact bootJar
        artifact sourceJar // because of bootJar, we need to put these back (sourceJar and javadocJar)
        artifact javadocJar
      }
    }
  }
}

dependencies {
  implementation 'org.springframework.boot:spring-boot-starter-actuator'
  implementation 'org.springframework.boot:spring-boot-starter-webflux'
  implementation 'io.micrometer:micrometer-registry-prometheus'
  implementation project(':prometheus-rsocket-spring')

  implementation 'io.rsocket:rsocket-micrometer'
  implementation 'io.rsocket:rsocket-transport-netty'

  implementation 'org.xerial.snappy:snappy-java:latest.release'
  annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

  testImplementation 'org.springframework.boot:spring-boot-starter-test'
  testImplementation 'io.projectreactor:reactor-test'
}

if (hasProperty('DOCKER_USER') && hasProperty('DOCKER_PASSWORD')) {
  bootBuildImage {
    imageName = "${findProperty('DOCKER_USER')}/prometheus-rsocket-proxy:${project.version}"
    environment = ["BP_JVM_VERSION" : "17.*"]
    docker {
      publishRegistry {
        username = findProperty('DOCKER_USER')
        password = findProperty('DOCKER_PASSWORD')
        email = 'tludwig@pivotal.io'
      }
    }
  }
}
